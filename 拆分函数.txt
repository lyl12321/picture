Sub 拆分工作表()
 
    Application.DisplayAlerts = False  '不显示错误提示框
    Application.ScreenUpdating = False  '不闪屏
    
    Dim i As Integer   '辅助工作表变量
    Dim sh As Worksheet
    
    '删除多余的工作表
    If Sheets.Count > 1 Then
        For i = Worksheets.Count To 2 Step -1
            Worksheets(i).Delete
        Next i
    End If
    
    '对信息表中数据按照班级排序，之后按照班级拆分进新的工作表
    Dim irow As Integer '定义一共需要处理的行号
    Dim istart As Integer  '定位起始行数变量
    
    irow = Range("A" & Rows.Count).End(xlUp).Row '计算一共需要处理的行号
    If irow > 2 Then
    
        Range("A2:N" & irow).Sort Range("N1"), xlAscending  '对信息区域进行排序，不能含标题
        istart = 2
        For i = 2 To irow
            With Worksheets("2019年团员信息表")  '指定活动工作表
            
            If .Range("N" & i).Value <> .Range("N" & i + 1).Value Then   '判断是否为同一班级
            
                Worksheets.Add after:=Worksheets(Sheets.Count)  '新建工作表
                Set sh = Worksheets(Worksheets.Count) '指定工作表给变量
                sh.Name = .Range("N" & i).Value  '以班级命名工作表
                .Range("A1:N1").Copy sh.Range("A1:N1") '复制标题到新建工作表中
                .Range("A" & istart & ":N" & i).Copy sh.Range("A2") '复制内容到工作表中
                sh.Columns.AutoFit  '设置自动列宽
                
                istart = i + 1
            End If
            End With
        Next i
    
    End If
    
    Worksheets("2019年团员信息表").Select  '回到第一个工作表
    
    Application.ScreenUpdating = True  '恢复闪屏默认设置
    Application.DisplayAlerts = True   '恢复提示框默认设置
End Sub


